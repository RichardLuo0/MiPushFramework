apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.github.wurensen.android-aspectjx'
apply plugin: 'org.greenrobot.greendao'


android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.xiaomi.xmsf"
        minSdkVersion rootProject.ext.minSdkVersion
        // Keep alive
        targetSdkVersion rootProject.ext.targetSdkVersion
        // XMPush sdk requires XMPushService not exported, but sdk will start exported XMPushService after
        // XMSF version code 106. SDK will also check version code must >= 105.
        // @see com.xiaomi.mipush.sdk.PushServiceClient#register
        // @see com.xiaomi.mipush.sdk.PushServiceClient#createServiceIntent
        // @see com.xiaomi.mipush.sdk.PushServiceClient#getPushServiceName
        versionCode 1003003000
        versionName rootProject.ext.versionName
        archivesBaseName = "xmsf-v${versionName}"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'x86', "arm64-v8a", "x86_64"
        }

        buildConfigField "String", "GIT_TAG", "\"" + rootProject.ext.gitTag + "\""
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
        }
    }

    flavorDimensions = ["version"]
    productFlavors {
        normal {
            dimension "version"
        }
        vc105 {
            dimension "version"
            versionCode = 105
        }
        prebuilt {
            dimension "version"
        }
    }
    androidComponents {
        onVariants(selector().all()) { variant ->
            if(variant.name.startsWith("prebuilt")) {
                variant.androidResources.noCompress.add('dex')
            }
        }
    }

    aspectjx {
        enabled = false
    }

    greendao {
        // Must upgrade version code!
        schemaVersion 17
        daoPackage 'top.trumeet.mipush.provider.gen.db'
    }
    namespace 'top.trumeet.mipush.provider'

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
            enableV3Signing = true
            enableV4Signing = true

            def locale = project.rootProject.file(".yuuta.jks")
            def keystorePwd = System.getenv("KEYSTORE_PASS")
            def alias = System.getenv("ALIAS_NAME")
            def pwd = System.getenv("ALIAS_PASS")
            if (project.rootProject.file('local.properties').exists()) {
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                locale = properties.getProperty("KEY_LOCATE")
                keystorePwd = properties.getProperty("KEYSTORE_PASSWORD")
                alias = properties.getProperty("KEYSTORE_ALIAS")
                pwd = properties.getProperty("KEY_PASSWORD")
            }

            if (locale != null) {
                storeFile file(locale)
                storePassword keystorePwd
                keyAlias alias
                keyPassword pwd
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }


    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.0"
    }
    namespace 'com.xiaomi.xmsf'
}

android.applicationVariants.all { variant ->
    String taskName = String.format("remove%sStubClasses", variant.name.capitalize())
    task "${taskName}" {
        doLast { delete "${buildDir}/intermediates/classes/${variant.name}" + "/android" }
    }
    variant.getProcessJavaResourcesProvider().get().dependsOn(taskName)
}

dependencies {
// modules {
    implementation project(':common')
    implementation project(':condom')
    implementation project(':mipush_hook')
    compileOnly files(project(':mipush_hook').ext.mipushLib)
// }

// feature dependencies {
    // Log
    implementation('com.elvishew:xlog:1.11.0', {
        exclude group: 'com.android.support'
    })

    implementation 'com.github.heruoxin:IceBox-SDK:55dd857d74'
    implementation 'com.google.code.gson:gson:2.10.1'

    // The core module that provides APIs to a shell
    // implementation "com.github.topjohnwu.libsu:core:5.0.5"

    // https://mvnrepository.com/artifact/com.github.promeg/tinypinyin
    implementation 'com.github.promeg:tinypinyin:2.0.3'

    // GreenDao
    implementation 'org.greenrobot:greendao:3.3.0'
// }

// Test {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation("org.mockito:mockito-android:4.11.0")
    testImplementation("org.mockito:mockito-core:4.11.0")
    testImplementation("org.mockito:mockito-inline:4.11.0")
// }

// Base {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'


    implementation "androidx.compose.ui:ui:1.4.0"
    implementation "androidx.compose.material3:material3:1.0.0"
    implementation "androidx.compose.ui:ui-tooling-preview:1.4.0"
    implementation "androidx.compose.ui:ui-tooling:1.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.activity:activity-compose:1.7.0"
    implementation 'androidx.navigation:navigation-compose:2.6.0'
    implementation "org.jetbrains:markdown:0.7.3"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.30.1"

    // Support
    implementation 'androidx.appcompat:appcompat:1.5.0'
// }


}