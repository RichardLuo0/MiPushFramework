// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.8.0'

    repositories {
        maven { url = uri("https://maven.aliyun.com/repository/central") }
        maven { url = uri("https://maven.aliyun.com/repository/public") }
        maven { url = uri("https://maven.aliyun.com/repository/gradle-plugin") }
        maven { url = uri("https://maven.aliyun.com/repository/google") }

        mavenCentral()
        maven { url 'https://jitpack.io' }
        gradlePluginPortal()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'org.greenrobot:greendao-gradle-plugin:3.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.github.wurensen:gradle-android-plugin-aspectjx:3.3.2'
    }

}

allprojects {
    repositories {
        maven { url = uri("https://maven.aliyun.com/repository/central") }
        maven { url = uri("https://maven.aliyun.com/repository/public") }
        maven { url = uri("https://maven.aliyun.com/repository/gradle-plugin") }
        maven { url = uri("https://maven.aliyun.com/repository/google") }

        mavenCentral()
        maven { url 'https://jitpack.io' }
        gradlePluginPortal()
        google()
    }

    gradle.taskGraph.whenReady {
        tasks.each { task ->
            if (task.name.equals('mockableAndroidJar')) {
                task.enabled = false
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    String versionName  = project.findProperty('versionName') ?: getVersionNameFromGit() ?: "VersionNameError"
    versionName = increaseVersionForPreRelease(versionName)
    return versionName.replaceFirst("^v", "")
}

private static String increaseVersionForPreRelease(String versionName) {
    if (isPreReleaseVersion(versionName)) {
        return versionName.replaceFirst("(\\d+\\.\\d+\\.)(\\d+)", {
            return it[1] + (it[2].toInteger() + 1)
        })
    }
    return versionName
}

private static boolean isPreReleaseVersion(String versionName) {
    versionName.contains('-')
}

private String getVersionNameFromGit() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            // v0.3.10-187-gef4be8c9-dirty
            commandLine 'git', 'describe', '--tags', '--dirty', '--exclude', 'v*-*'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

ext {
    minSdkVersion = 24
    compileSdkVersion = 33
    targetSdkVersion = 30
    pushVersionCode = 7

    gitVersionCode = getVersionCode()
    gitVersionName = getVersionName()
    snapshot = false
    versionBase = gitVersionName
    versionName = snapshot ? versionBase + "-SNAPSHOT" : versionBase
    gitTag = versionName

    // Work
    work_version = "1.0.0-alpha05"
}

task exportVersion(type: Exec) {
    commandLine 'sh'
    doLast {
        file("$projectDir/version.txt").text = """$versionName"""
        file("$projectDir/gitTag.txt").text = """$gitTag"""
    }
}
